buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+"
}
}
plugins {
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
}

version = project.mod_version
group = project.modGroup
archivesBaseName = project.modBaseName

String b = ""
if (project.hasProperty("teamcity")) {
    b = System.getenv("BUILD_NUMBER")
}

if (b == null || b == "") {
    version = "${project.minecraft_version}-${project.mod_version}"
} else {
    version = "${project.minecraft_version}-${project.mod_version}-" + b
}
minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: "stable", version: "39-1.12"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {
            workingDirectory project.file('run_server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            mods {
                mymod {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url = "https://maven.minecraftforge.net/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "CurseMaven"
        url "https://www.cursemaven.com"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2855"
    implementation fg.deobf("curse.maven:refined-storage-243076:2690877")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version
    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": project.minecraft_version
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": "modBaseName",
                "Specification-Vendor": "MrDogit, Buuz135",
                "Specification-Version": project.mod_version, // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" : "MrDogit, Buuz135",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

//When Forge 1.12 loads mods from a directory that's been put on the classpath, it expects to find resources in the same directory.
//Default Gradle behavior puts resources in ./build/resources/main instead of ./build/classes/main/java. Let's change that.
sourceSets.all { it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next() }

tasks.register("serverJar", Jar) {
    from sourceSets.main.output
    archiveClassifier.set("server")
}

tasks.matching { it.name == "runServer" && it instanceof JavaExec }.configureEach {
    dependsOn("serverJar")
    doFirst {
        def srv   = tasks.named("serverJar").get().archiveFile.get().asFile
        def normal = tasks.named("jar").get().archiveFile.get().asFile
        // выкидываем обычный jar из уже собранного FG classpath
        def filtered = files(classpath.files.findAll { it != normal })
        classpath = files(srv) + filtered
    }
}